plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.eightbit.samsprung"
        minSdk 30
        targetSdk 31
        versionCode 186
        versionName "1.8.6"

        vectorDrawables {
            useSupportLibrary = true
        }
        renderscriptTargetApi 31
        renderscriptSupportModeEnabled true

        def commit = 'git rev-parse --short HEAD'.execute().text.trim()
        buildConfigField "String", "COMMIT", "\""+commit+"\""
        project.archivesBaseName = "SamSprung-" + commit
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a"
            universalApk false
        }
    }

    flavorDimensions "source"
    productFlavors {
        github {
            getIsDefault().set(true)
        }

        google { }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith release
            debuggable true
            try {
                def keystoreProps = new Properties()
                keystoreProps.load(new FileInputStream(rootProject.file("keystore.properties")))
                signingConfigs {
                    debug {
                        keyAlias keystoreProps['keyAlias']
                        keyPassword keystoreProps['keyPassword']
                        storeFile file(keystoreProps['storeFile'])
                        storePassword keystoreProps['storePassword']
                    }
                }
                buildTypes.debug.signingConfig signingConfigs.debug
            } catch (FileNotFoundException ignored) { }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    lint {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs[0].outputFileName = project.archivesBaseName + ".apk"
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.activity:activity-ktx:1.6.0-alpha04'
    implementation 'com.android.billingclient:billing:5.0.0'
    implementation 'com.google.android.play:core:1.10.3'
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation 'com.github.SamSprung:android-issue-reporter:c83f4b9'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}
